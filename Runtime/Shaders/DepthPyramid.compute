#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
#include "./Common.hlsl"

#pragma only_renderers d3d11 playstation xboxone xboxseries vulkan metal switch

#pragma kernel KCopyDepthTextureToMipChain  KERNEL_SIZE=8
#pragma kernel KDepthDownsample8DualUav     KERNEL_SIZE=8  KERNEL_NAME=KDepthDownsample8DualUav

TEXTURE2D_X(_CameraDepthTexture);
RW_TEXTURE2D_X(float, _DepthMipChain);

CBUFFER_START(DepthPyramid)
    uint4 _SrcOffsetAndLimit; // {x, y, w - 1, h - 1}
    uint4 _DstOffset;         // {x, y, 0, 0}
CBUFFER_END

#if UNITY_REVERSED_Z
# define MIN_DEPTH(l, r) max(l, r)
#else
# define MIN_DEPTH(l, r) min(l, r)
#endif

[numthreads(KERNEL_SIZE, KERNEL_SIZE, 1)]
void KCopyDepthTextureToMipChain(uint3 dispatchThreadId : SV_DispatchThreadID)
{
    _DepthMipChain[dispatchThreadId.xy].r = _CameraDepthTexture[dispatchThreadId.xy].r;
}

// Downsample a depth texture by taking the min value of sampled pixels
// The size of the dispatch is (DstMipSize / KernelSize).
[numthreads(KERNEL_SIZE, KERNEL_SIZE, 1)]
void KERNEL_NAME(uint3 dispatchThreadId : SV_DispatchThreadID)
{
    // UNITY_XR_ASSIGN_VIEW_INDEX(dispatchThreadId.z);

    uint2 srcOffset = _SrcOffsetAndLimit.xy;
    uint2 srcLimit  = _SrcOffsetAndLimit.zw;
    uint2 dstOffset = _DstOffset.xy;

    // Upper-left pixel coordinate of quad that this thread will read
    uint2 srcPixelUL = srcOffset + (dispatchThreadId.xy << 1);

    float p00 = _DepthMipChain[COORD_TEXTURE2D_X(min(srcPixelUL + uint2(0u, 0u), srcLimit))];
    float p10 = _DepthMipChain[COORD_TEXTURE2D_X(min(srcPixelUL + uint2(1u, 0u), srcLimit))];
    float p01 = _DepthMipChain[COORD_TEXTURE2D_X(min(srcPixelUL + uint2(0u, 1u), srcLimit))];
    float p11 = _DepthMipChain[COORD_TEXTURE2D_X(min(srcPixelUL + uint2(1u, 1u), srcLimit))];
    float4 depths = float4(p00, p10, p01, p11);

    // Select the closest sample
    float minDepth = MIN_DEPTH(MIN_DEPTH(depths.x, depths.y), MIN_DEPTH(depths.z, depths.w));

    _DepthMipChain[COORD_TEXTURE2D_X(dstOffset + dispatchThreadId.xy)] = minDepth;
}

#undef MIN_DEPTH
